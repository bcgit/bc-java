plugins {
  id "biz.aQute.bnd.builder" version "7.1.0"
}

dependencies {
    implementation project(':core')
    testImplementation project(':core')
    testImplementation files('../libs/unboundid-ldapsdk-6.0.8.jar')
}

evaluationDependsOn(":core")

sourceSets {
    main {
        java {
            srcDirs = ["${projectDir}/../core/src/main/java","src/main/java"]
        }
        resources {
            srcDirs= ["${projectDir}/../core/src/main/resources","src/main/resources"]
        }
    }


    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }
    java11 {
        java {
            srcDirs = ['src/main/jdk1.11']
        }
    }
    java15 {
        java {
            srcDirs = ['src/main/jdk1.15']
        }
    }
    java17 {
        java {
            srcDirs = ['src/main/jdk17']
        }
    }
    java25 {
        java {
            srcDirs = ['src/main/jdk25']
        }
    }

}

dependencies {

    java9Implementation files([sourceSets.main.output.classesDirs]) {
        builtBy compileJava
    }

    java11Implementation files([
            sourceSets.main.output.classesDirs,
            sourceSets.java9.output.classesDirs]) {
        builtBy compileJava9Java
    }

    java15Implementation files([
            sourceSets.main.output.classesDirs,
            sourceSets.java9.output.classesDirs,
            sourceSets.java11.output.classesDirs]) {
        builtBy compileJava11Java
    }

    java17Implementation files([
            sourceSets.main.output.classesDirs,
            sourceSets.java9.output.classesDirs,
            sourceSets.java11.output.classesDirs,
            sourceSets.java15.output.classesDirs]) {
        builtBy compileJava15Java
    }

    java25Implementation files([
            sourceSets.main.output.classesDirs,
            sourceSets.java9.output.classesDirs,
            sourceSets.java11.output.classesDirs,
            sourceSets.java15.output.classesDirs,
            sourceSets.java17.output.classesDirs]) {
        builtBy compileJava17Java
    }
}


compileJava {
    options.release = 8;
}

compileJava9Java {
    options.release = 9
    options.sourcepath = files(['../core/src/main/java', 'src/main/java', 'src/main/jdk1.9'])
}

compileJava11Java {
    options.release = 11
    options.sourcepath = files(['src/main/java', 'src/main/jdk1.11'])
}

compileJava15Java {
    options.release = 15
    options.sourcepath = files(['src/main/java', 'src/main/jdk1.15'])
}

compileJava17Java {
    targetCompatibility = 17
    sourceCompatibility = 17
    options.sourcepath = files(['src/main/java', 'src/main/jdk17'])
}

compileJava25Java {
    options.release = 25
    options.sourcepath = files(['src/main/java', 'src/main/jdk25'])
}


task sourcesJar(type: Jar) {
    archiveBaseName="bcprov"
    archiveAppendix="${vmrange}"
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    exclude("**/*.so")
    into('META-INF/versions/9') {
        from sourceSets.java9.allSource
    }
    into('META-INF/versions/11') {
        from sourceSets.java11.allSource
    }
    into('META-INF/versions/15') {
        from sourceSets.java15.allSource
    }
    into('META-INF/versions/17') {
        from sourceSets.java17.allSource
    }
    into('META-INF/versions/25') {
        from sourceSets.java25.allSource
    }
}

jar {
    jar.archiveBaseName="bcprov"
    jar.archiveAppendix="${vmrange}"

    from sourceSets.main.output
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    into('META-INF/versions/15') {
        from sourceSets.java15.output
    }
    into('META-INF/versions/17') {
        from sourceSets.java17.output
    }
    into('META-INF/versions/25') {
        from sourceSets.java25.output
    }
    String v = "${rootProject.extensions.ext.bundle_version}"
    manifest.attributes('Multi-Release': 'true')
    manifest.attributes('Bundle-Name': 'bcprov')
    manifest.attributes('Bundle-SymbolicName': 'bcprov')
    manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
    manifest.attributes('Export-Package': "!org.bouncycastle.internal.*,org.bouncycastle.*;version=${v}")
    manifest.attributes('Import-Package': 'java.*;resolution:=optional,javax.*;resolution:=optional')
    manifest.attributes('Bundle-Version': "${v}")
    manifest.attributes('Permissions': 'all-permissions')
    manifest.attributes('Codebase': '*')
    manifest.attributes('Application-Library-Allowable-Codebase': '*')
    manifest.attributes('Caller-Allowable-Codebase': '*')
    manifest.attributes('Trusted-Library': 'true')
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName="bcprov"
    archiveAppendix="${vmrange}"
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}



sourceSets {
    test11 {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += test.output
            srcDir(files("src/test/jdk1.11"))
        }
    }

    test15 {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += test.output
            srcDir(files("src/test/jdk1.15"))
        }
    }

    test17 {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += test.output
            srcDir(files("src/test/jdk17"))
        }
    }

    test25 {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += test.output
            srcDir(files("src/test/jdk25"))
        }
    }
}

dependencies {
    java9Implementation project(':core')
    java11Implementation project(':core')
    java15Implementation project(':core')
    java17Implementation project(':core')
    java25Implementation project(':core')
}

dependencies {
    test11Implementation group: 'junit', name: 'junit', version: '4.13.2'
    test15Implementation group: 'junit', name: 'junit', version: '4.13.2'
    test17Implementation group: 'junit', name: 'junit', version: '4.13.2'
    test25Implementation group: 'junit', name: 'junit', version: '4.13.2'
    test11Implementation files('../libs/unboundid-ldapsdk-6.0.8.jar')
    test15Implementation files('../libs/unboundid-ldapsdk-6.0.8.jar')
    test17Implementation files('../libs/unboundid-ldapsdk-6.0.8.jar')
    test25Implementation files('../libs/unboundid-ldapsdk-6.0.8.jar')
    test11Implementation(project(":core"))
    test15Implementation(project(":core"))
    test17Implementation(project(":core"))
    test25Implementation(project(":core"))
    test25Implementation sourceSets.java25.output


}




compileTest11Java {
    options.release = 11
    options.sourcepath = files(['src/test/java', 'src/test/jdk1.11'])
}

compileTest15Java {
    options.release = 15
    options.sourcepath = files(['src/test/java', 'src/test/jdk1.15'])
}

compileTest17Java {
    targetCompatibility = 17
    sourceCompatibility = 17
    options.sourcepath = files(['src/test/java', 'src/test/jdk17'])
}

compileTest25Java {
    options.release = 25
    options.sourcepath = files(['src/test/java', 'src/test/jdk25'])
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.bouncycastle'
            artifactId = "bcprov-$vmrange"
            from components.java

            artifact(javadocJar)
            artifact(sourcesJar)
        }

    }
}

configurations {
    test11Implementation.extendsFrom testImplementation
    test15Implementation.extendsFrom testImplementation
    test17Implementation.extendsFrom testImplementation
    test25Implementation.extendsFrom testImplementation
}


test {
    jvmArgs = ['-Dtest.java.version.prefix=any']
}


task test8(type: Test) {
    onlyIf {System.getenv("BC_JDK8") != null}
    dependsOn(jar)

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = false

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8)
    }

    jvmArgs = ['-Dtest.java.version.prefix=1.8']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test11(type: Test) {
    onlyIf {System.getenv("BC_JDK11") != null}
    dependsOn(jar)

    testClassesDirs = sourceSets.test11.output.classesDirs
    classpath = sourceSets.test11.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = false

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(11)
    }

    jvmArgs = ['-Dtest.java.version.prefix=11']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test15(type: Test) {

    // This is testing the 1.15 code base
    onlyIf {System.getenv("BC_JDK17") != null}
    dependsOn jar

    testClassesDirs = sourceSets.test15.output.classesDirs
    classpath = sourceSets.test15.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = false

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    jvmArgs = ['-Dtest.java.version.prefix=17']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test17(type: Test) {

    // This is testing the 17 code base
    onlyIf {System.getenv("BC_JDK17") != null}
    dependsOn jar

    testClassesDirs = sourceSets.test17.output.classesDirs
    classpath = sourceSets.test17.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = false

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    jvmArgs = ['-Dtest.java.version.prefix=17']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test25(type: Test) {

    // This is testing the 25 code base
    onlyIf {System.getenv("BC_JDK25") != null}
    dependsOn jar

    testClassesDirs = sourceSets.test25.output.classesDirs
    classpath = sourceSets.test25.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = false

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(25)
    }

    jvmArgs = ['-Dtest.java.version.prefix=25']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

if (System.getenv("BC_JDK8") != null) {
    System.out.println("${project.name}: Adding test8 as dependency for test task because BC_JDK8 is defined")
    test.dependsOn("test8")
}

if (System.getenv("BC_JDK11") != null) {
    System.out.println("${project.name}: Adding test11 as dependency for test task because BC_JDK11 is defined")
    test.dependsOn("test11")
}

if (System.getenv("BC_JDK17") != null) {
    System.out.println("${project.name}: Adding test15 as dependency for test task because BC_JDK17 is defined")
    test.dependsOn("test15")
}

if (System.getenv("BC_JDK17") != null) {
    System.out.println("${project.name}: Adding test17 as dependency for test task because BC_JDK17 is defined")
    test.dependsOn("test17")
}
if (System.getenv("BC_JDK25") != null) {
    System.out.println("${project.name}: Adding test25 as dependency for test task because BC_JDK25 is defined")
    test.dependsOn("test25")
}



