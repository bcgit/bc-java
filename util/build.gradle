
plugins {
  id "biz.aQute.bnd.builder" version "7.1.0"
}

jar.archiveBaseName = "bcutil-$vmrange"


sourceSets {
    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }

}


dependencies {

    implementation project(':prov')

    testImplementation group: 'junit', name: 'junit', version: '4.11'

    java9Implementation files([sourceSets.main.output.classesDirs]) {
        builtBy compileJava
    }
    java9Implementation project(':prov')


}

evaluationDependsOn(":prov")

compileJava {
    options.release = 8
}


compileJava9Java {


    def prov_jar="${project(":prov").jar.outputs.files.getFiles().getAt(0)}"

    options.compilerArgs += [
            '--module-path', "${prov_jar}"
    ]

    sourceCompatibility = 9
    targetCompatibility = 9
    options.sourcepath = files(['src/main/java', 'src/main/jdk1.9'])
}


jar {
    from sourceSets.main.output
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    String packages = 'org.bouncycastle.asn1.{bsi|cmc|cmp|cms|crmf|cryptlib|dvcs|eac|edec|esf|ess|est|gnu|iana|icao|isara|isismtt|iso|kisa|microsoft|misc|mozilla|nsri|ntt|oiw|rosstandart|smime|tsp}.*'
    String v = "${rootProject.extensions.ext.bundle_version}"
    manifest.attributes('Multi-Release': 'true')
    manifest.attributes('Bundle-Name': 'bcutil')
    manifest.attributes('Bundle-SymbolicName': 'bcutil')
    manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
    manifest.attributes('Export-Package': "${packages};version=${v},org.bouncycastle.oer.*;version=${v}")
    manifest.attributes('Import-Package': "java.*;resolution:=optional,javax.*;resolution:=optional,!${packages},!org.bouncycastle.oer.*,org.bouncycastle.*;version=\"[${v},${maxVersion})\"")
    manifest.attributes('Bundle-Version': "${v}")
    manifest.attributes('Permissions': 'all-permissions')
    manifest.attributes('Codebase': '*')
    manifest.attributes('Application-Library-Allowable-Codebase': '*')
    manifest.attributes('Caller-Allowable-Codebase': '*') 
    manifest.attributes('Trusted-Library': 'true')
}

jar.doLast {
    if (System.getenv("SIGNJAR_CMD") != null ) {
        exec {
            commandLine(System.getenv("SIGNJAR_CMD"),jar.archiveFile)
        }
    }
}



task sourcesJar(type: Jar) {
    archiveBaseName = jar.archiveBaseName
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    exclude("**/*.so")
    into('META-INF/versions/9') {
        from sourceSets.java9.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveBaseName = jar.archiveBaseName
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.bouncycastle'
            artifactId = "bcutil-$vmrange"
            from components.java

            artifact(javadocJar)
            artifact(sourcesJar)
        }

    }
}


test {
    forkEvery = 1;
    maxParallelForks = 8;
}

