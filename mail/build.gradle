plugins {
  id "biz.aQute.bnd.builder" version "7.0.0"
}

jar.archiveBaseName = "bcmail-$vmrange"

sourceSets {
    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }
}

dependencies {
    implementation project(':prov')
    implementation project(':util')
    implementation project(':pkix')

    implementation group: 'javax.mail', name: 'mail', version: '1.4'

    java9Implementation files(sourceSets.main.output.classesDirs) {
        builtBy compileJava
    }
}

evaluationDependsOn(":prov")
evaluationDependsOn(":util")
evaluationDependsOn(":pkix")

compileJava {

    options.release = 8
}


compileJava9Java {

    options.release = 9
    def prov_jar="${project(":prov").jar.outputs.files.getFiles().getAt(0)}"
    def util_jar="${project(":util").jar.outputs.files.getFiles().getAt(0)}"
    def pkix_jar="${project(":pkix").jar.outputs.files.getFiles().getAt(0)}"

    options.compilerArgs += [
            '--module-path', "${prov_jar}${File.pathSeparator}${util_jar}${File.pathSeparator}${pkix_jar}"
    ]

    options.sourcepath = files(['src/main/java', 'src/main/jdk1.9'])
}


jar {
    from sourceSets.main.output
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    String v = "${rootProject.extensions.ext.bundle_version}"
    manifest.attributes('Multi-Release': 'true')
    manifest.attributes('Bundle-Name': 'bcmail')
    manifest.attributes('Bundle-SymbolicName': 'bcmail')
    manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
    manifest.attributes('Export-Package': "org.bouncycastle.mail.*;version=${v}")
    manifest.attributes('Import-Package': "java.*;resolution:=optional,javax.*;resolution:=optional,!org.bouncycastle.mail.*,org.bouncycastle.*;version=\"[${v},${maxVersion})\"")
    manifest.attributes('Bundle-Version': "${v}")
}

task sourcesJar(type: Jar) {
    archiveBaseName = jar.archiveBaseName
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    into('META-INF/versions/9') {
        from sourceSets.java9.allSource
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveBaseName = jar.archiveBaseName
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    forkEvery = 1;
    maxParallelForks = 8;
    jvmArgs = ['-Dtest.java.version.prefix=any']
}

compileJava9Java.dependsOn([":prov:jar", ":util:jar",":pkix:jar"])

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.bouncycastle'
            artifactId = "bcmail-$vmrange"
            from components.java


            artifact(javadocJar)
            artifact(sourcesJar)
        }


    }
}